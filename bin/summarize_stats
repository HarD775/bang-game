#!/usr/bin/perl -w
#
# $Id$
#
# Grinds the Bang daily server log and outputs a summary of various usage
# statistics. This will eventually be replaced with a more sophisticated tool
# that stores this information in a database (perhaps factored out of the
# Yohoho stats processing code).

my $limit = 10;

my %firsttimers;

my %usertime;
my %usersess;
my %userrounds;
my %userearn;

my $tottime = 0;
my $totsess = 0;
my $newtime = 0;
my $newsess = 0;

my %byscen;
my %bytut;
my %byteamsize;

my $totgametime = 0;
my $ratgames = 0;
my $unratgames = 0;

my %games;
my %rounds;
my %time;

my $totearn = 0;
my $newearn = 0;

my %leavers;
my %discons;
my %logoffs;

# grind and process the raw data
while (<>) {
    chomp;
    if (/first_timer (\S+)/) {
        $firsttimers{$1} = $1;
    } elsif (/session_end (\S+) ctime:(\d+)/) {
        $usertime{$1} += $2;
        $usersess{$1}++;
        $tottime += $2;
        $totsess++;
        if (defined $firsttimers{$1}) {
            $newtime += $2;
            $newsess++;
        }

    } elsif (/game_ended t:(\d+) s:(\S+) ts:(\d+) (.*)/) {
        my $gsecs = $1;
        my $tsize = $3;
        my $info = $4;
        my @scens = split(/,/, $2);
        my $rcount = scalar @scens;

        # TODO change when we unfuck the formatting
        my $rtype;
        if ($info =~ s/ r:true//) {
            $rtype = "rated";
        } elsif ($info =~ s/ r:false//) {
            $rtype = "unrated";
        } else {
            warn "Bogus info? $info\n";
            next;
        }

        my $humans = 0;
        my @pinfos = split(/,/, $info);
        foreach $pinfo (@pinfos) {
            my @bits = split(/:/, $pinfo);
            my $username = $bits[0];
            $humans++ unless ($username =~ /\(tin_can\)/);
        }

        my $tutorial = 0;
        my $type;
        if ($rcount == 1 && length($scens[0]) > 2) {
            $tutorial = 1;
            $type = "tutorial";
        } elsif ($humans > 1) {
            $type = "human";
        } else {
            $type = "tincan";
        }

        $games{$type}++;
        $time{$type} += $gsecs;

        if ($tutorial) {
            foreach $scen (@scens) {
                $bytut{$scen}++;
            }
        } else {
            foreach $scen (@scens) {
                $byscen{$scen}++;
            }
        }

        # these are only recorded for non-tutorial games
        if (!$tutorial) {
            $games{$rtype}++;
            $rounds{$rtype} += $rcount;
            $byteamsize{$tsize} += $rcount;
        }

        foreach $pinfo (@pinfos) {
            my @bits = split(/:/, $pinfo);
            my $username = $bits[0];
            if ($username =~ s/\#$//) {
                note_leaver(\%leavers, $humans, $tutorial);
            }
            if ($username =~ s/!$//) {
                note_leaver(\%discons, $humans, $tutorial);
            }
            if ($username =~ s/\*$//) {
                note_leaver(\%logoffs, $humans, $tutorial);
            }
            if ($tutorial) {
                $userrounds{"tutorial"}{$username} += $rcount;
            } else {
                $userrounds{"total"}{$username} += $rcount;
                $userrounds{"rated"}{$username} += $rcount
                    if ($rtype eq "rated");
                my $earnings = (@bits > 3) ? $bits[3] : 0;
                $userearn{$username} += $earnings;
                $totearn += $earnings;
                if (defined $firsttimers{$username}) {
                    $newearn += $earnings;
                    $newrounds += $rcount;
                }
            }

            # we note the number of rounds played for each player in the game
            $rounds{$type} += $rcount;
        }
    }
}

printf("Type        Games\n");
printf("vs Humans   %5d\n", $games{"human"});
printf("vs Tin Cans %5d\n", $games{"tincan"});
printf("Tutorials   %5d\n", $games{"tutorial"});
print "\n";

my $totrounds = $rounds{"human"} + $rounds{"tincan"};

# summarize sessions, rounds played, earnings, etc.
my $ucount = keys %usersess;
my $ncount = keys %firsttimers;
my $totmins = $tottime/60;
my $newmins = $newtime/60;
print "Username    Sess.  Minutes /sess  Rounds  Earnings /round\n";
printf("all %6d %6d  %7.0f    %2.0f  %6d  %8d\n",
       $ucount, $totsess, $totmins, $totmins/$totsess, $totrounds, $totearn);
printf("noobs %4d %6d  %7.0f    %2.0f  %6d  %8d\n",
       $ncount, $newsess, $newmins, $newmins/$newsess, $newrounds, $newearn);
printf("all  (avg)    %2.1f  %7.0f           %2.1f  %8d   %3.0f\n",
       $totsess/$ucount, $totmins/$ucount, $totrounds/$ucount,
       $totearn/$ucount, $totearn/$totrounds);
printf("noob (avg)    %2.1f  %7.0f           %2.1f  %8d   %3.0f\n",
       $newsess/$ucount, $newmins/$ucount, $newrounds/$ucount,
       $newearn/$ucount, $newearn/$newrounds);
print "\n";

# top ten by earnings
print "                    - top by earnings -\n";
sum_users(sort { $userearn{$b} - $userearn{$a} } keys %userearn);

# top ten by rounds played
print "                  - top by rounds played -\n";
sum_users(sort { $userrounds{"total"}{$b} - $userrounds{"total"}{$a} }
          keys %{$userrounds{"total"}});

# top ten by minutes online
print "                 - top by minutes online -\n";
sum_users(sort { $usertime{$b} - $usertime{$a} } keys %usertime);

# break down disconnect/logoff/leave stats
print "         vs. Tincans   vs. Humans    Tutorial\n";
sum_leavers("Discons", \%discons);
sum_leavers("Logoffs", \%logoffs);
sum_leavers("Leavers", \%leavers);
print "\n";

# break down games played by scenario
printf("Scenario         Rounds\n");
printf("%-15s  %6d\n", "(total)", $totrounds);
printf("%-15s  %6d\n", "(unrated)", $rounds{"unrated"});
printf("%-15s  %6d\n", "(rated)", $rounds{"rated"});
foreach $scen (sort { $byscen{$b} - $byscen{$a} } keys %byscen) {
    printf("%-15s  %6d\n", $scen, $byscen{$scen});
}
print "\n";

# break down games played by scenario
printf("Tutorial         Rounds\n");
foreach $scen (sort { $bytut{$b} - $bytut{$a} } keys %bytut) {
    printf("%-15s  %6d\n", $scen, $bytut{$scen});
}
print "\n";

# break down games played by team size
printf("Units            Rounds\n");
foreach $size (sort { $byteamsize{$b} - $byteamsize{$a} } keys %byteamsize) {
    printf("%-15d  %6d\n", $size, $byteamsize{$size});
}
print "\n";

sub sum_users {
    print "Username     Sess  Mins /sess  Rounds  Earn /round\n";
    my $idx = 0;
    foreach $user (@_) {
        next if ($user =~ /\(tin_can\)/);
        my $mins = safe_get($user, \%usertime) / 60;
        my $avgsess = safe_div($mins, safe_get($user, \%usersess));
        my $games = safe_get($user, $userrounds{"total"});
        my $rgames = safe_get($user, $userrounds{"rated"});
        my $earn = safe_get($user, \%userearn);
        printf("%-12s %4d  %4.0f  %4.0f   %2d/%2d %5d   %3.0f\n",
               $user, safe_get($user, \%usersess), $mins, $avgsess,
               $games, $rgames, $earn, safe_div($earn, $games));
        last if (++$idx == $limit);
    }
    print "\n";
}

sub safe_get {
    my ($key, $hash) = @_;
    return defined $hash->{$key} ? $hash->{$key} : 0;
}

sub safe_div {
    my ($num, $denom) = @_;
    return ($denom != 0) ? $num/$denom : 0;
}

sub note_leaver {
    my ($hash, $humans, $tutorial) = @_;
    if ($tutorial) {
        $hash->{"tutorial"}++;
    } elsif ($humans > 1) {
        $hash->{"human"}++;
    } else {
        $hash->{"tincan"}++;
    }
}

sub sum_leavers {
    my ($what, $hash) = @_;
    my $tinrounds = ($totrounds - $rounds{"human"});
    my $cvalue = safe_get("tincan", $hash);
    my $crounds = $rounds{"tincan"};
    my $hvalue = safe_get("human", $hash);
    my $hrounds = $rounds{"human"};
    my $tvalue = safe_get("tutorial", $hash);
    my $trounds = $rounds{"tutorial"};
    printf("%s %4d (%4.1f%%) %4d (%4.1f%%) %4d (%4.1f%%)\n",
           $what,
           $cvalue, 100*$cvalue/$crounds,
           $hvalue, 100*$hvalue/$hrounds,
           $tvalue, 100*$tvalue/$trounds);
}
